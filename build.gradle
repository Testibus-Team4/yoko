/* Configure classpath to find 3rd party plugins */
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.1.0'
  }
}

ext {
  testCount = 0
  successfulTestCount = 0
  failedTestCount = 0
  skippedTestCount = 0
}

wrapper {
    jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
    distributionType = Wrapper.DistributionType.ALL
}

/**
 * Compute a hash no longer than <code>length</code> for the git repository.
 * @param length
 * @return the hash as a string
 */
def getGitHash(length) {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', "--short=$length", 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def testProjects = "osgi util rmi-impl core test testify".split(" ").collect{s->project(":yoko-" + s)}
def shipProjects = "osgi util spec-corba rmi-spec rmi-impl core jdk-supplement testify".split(" ").collect{s->project(":yoko-" + s)}

/* Standard configuration for each subproject */
subprojects {
  apply plugin: 'java'
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenLocal()
    maven { url "https://repository.apache.org/snapshots" }
    maven { url "https://repo.maven.apache.org/maven2" }
  }

  configurations {
    // declare a new configuration to map to Maven's provided scope
    provided.transitive = true
    compile.extendsFrom provided
    // declare a new configuration to use for spec API jars
    spec.transitive = true
    compile.extendsFrom spec
    // declare a test library configuration
    testLib
    testImplementation.extendsFrom testLib
  }

  sourceSets.main.compileClasspath += configurations.provided
}

/* Standard configuration for subprojects with tests */
configure(testProjects) {
  // standardise the test deps here
  dependencies {
    testLib 'junit:junit:4.12'
    testLib 'org.mockito:mockito-core:2.22.0'
    testLib 'org.mockito:mockito-junit-jupiter:2.22.0'
    testLib "org.hamcrest:hamcrest:2.1"
    testLib "org.junit.jupiter:junit-jupiter:5.8.2"
    testLib "org.junit.platform:junit-platform-runner:1.8.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testRuntime "org.junit.vintage:junit-vintage-engine:5.8.2"
  }

  if (JavaVersion.current() < JavaVersion.VERSION_1_9) {
    // Add a task to copy the spec jars into a folder
    // to use with the endorsed standards mechanism
    task stageEndorsed(type: Sync) {
      from configurations.spec
      into temporaryDir
    }

    // Make all compilation tasks use the endorsed dir
    tasks.withType(JavaCompile) {
      dependsOn stageEndorsed
      options.compilerArgs << "-Djava.endorsed.dirs=$stageEndorsed.destinationDir"
    }

    // Make execute tasks use the endorsed dir
    tasks.withType(Test) {
      dependsOn stageEndorsed
      jvmArgs "-Djava.endorsed.dirs=$stageEndorsed.destinationDir"
    }
  } else {
    tasks.withType(Test) {
      jvmArgs "--illegal-access=permit"
    }
  }

  // ensure tests run under $buildDir somewhere
  test {
    apply plugin: 'jacoco'
    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
          html {
            enabled true
            destination file("$buildDir/../../reports/$project.name")
          }
        }
    }
    finalizedBy jacocoTestReport
    
    useJUnitPlatform()

    workingDir = "$buildDir/testWorkingDir"
    doFirst {workingDir.mkdirs()}
    testLogging {
      // set options for log level LIFECYCLE
      events "skipped", "failed", "passed", "standardOut"
      showExceptions true
      exceptionFormat "full"
      showCauses true
      showStackTraces true

      // set options for log level DEBUG and INFO
      debug {
        events "skipped", "failed", "started", "passed", "standardOut", "standardError"
        exceptionFormat "full"
      }
      info.events = debug.events
      info.exceptionFormat = debug.exceptionFormat

      afterTest { desc, result ->
        def totalTime = result.endTime - result.startTime
        println """"Test elapsed time","$desc.name",$totalTime"""
      }

      afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
          rootProject.testCount += result.testCount
          rootProject.successfulTestCount += result.successfulTestCount
          rootProject.failedTestCount += result.failedTestCount
          rootProject.skippedTestCount += result.skippedTestCount
          def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
          def startItem = '|  ', endItem = '  |'
          def repeatLength = startItem.length() + output.length() + endItem.length()
          println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
        def totalTime = result.endTime - result.startTime
        println """"Suite elapsed time","$desc.name",$totalTime"""
      }
    }
  }
}

/* Standard configuration for subprojects with shippable outputs */
configure(shipProjects) {
  apply plugin: 'osgi'
  apply plugin: 'maven-publish'
  group = 'org.apache.yoko'
  version = '1.5.0.' + getGitHash(10)
  publishing {
    publications {
      maven(MavenPublication) {
        artifact jar

        pom.withXml {
          def rootNode = asNode()

          // // specify packaging type as bundle (default is pom)
          // def packaging = root.packaging[0] ?:
          //                 root.appendNode('packaging')
          // packaging.setValue('bundle')

          def depsNode = rootNode.dependencies[0] ?:
                  rootNode.appendNode('dependencies')

          // declare 'provided' scope dependencies in the pom.xml
          // Note: the 'provided' scope is built into Maven,
          // but it is a custom configuration in this Gradle
          configurations.provided.allDependencies.each {
            def depNode = depsNode.appendNode('dependency')
            depNode.appendNode('groupId', it.group)
            depNode.appendNode('artifactId', it.name)
            depNode.appendNode('version', it.version)
            depNode.appendNode('scope', 'provided')
          }

          // declare compile scope dependencies in the pom.xml
          // as compile dependencies
          configurations.compile.allDependencies.each {
            def depNode = depsNode.appendNode('dependency')
            depNode.appendNode('groupId', it.group)
            depNode.appendNode('artifactId', it.name)
            depNode.appendNode('version', it.version)
            depNode.appendNode('scope', 'compile')
          }
        }
      }
    }
  }

  jar {
    dependsOn "generatePomFileForMavenPublication"
    // generate the maven dependency metadata
    into("/META-INF/maven/$project.group/$project.name") {
      from 'build/publications/maven'
      rename "pom-default.xml", "pom.xml"
    }
    manifest {
      instruction 'Bundle-DocURL'          , "https://geronimo.apache.org/maven/yoko/$project.version/$project.name"
      instruction 'Bundle-License'         , 'https://www.apache.org/licenses/LICENSE-2.0.txt'
      instruction 'Bundle-Vendor'          , 'The Apache Software Foundation'
      instruction 'Import-Package'         , 'sun.*;resolution:=optional,*'
    }
  }

  publish.dependsOn build
  // generated tasks need to be expressed as strings
  // because they don't exist yet when these lines are executed
  publish.dependsOn "publishMavenPublicationToMavenLocal"
  publish.dependsOn "publishToMavenLocal"
}

// Summarise the number of tests
gradle.buildFinished {
  if (testCount == 0) return
  def output = "FINAL RESULT: ${failedTestCount ? "FAILURE" : "SUCCESS"} (${testCount} tests, ${successfulTestCount} successes, ${failedTestCount} failures, ${skippedTestCount} skipped)"
  def startItem = '|  ', endItem = '  |'
  def repeatLength = startItem.length() + output.length() + endItem.length()
  println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
}
